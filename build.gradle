plugins {
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'org.jetbrains.dokka' version '0.9.17'
}

group 'org.sampl'
version '0.0.1'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    antlr 'org.antlr:antlr4:4.5' // use ANTLR version 4
    implementation 'com.github.SamChou19815:okaml-lib:0.4.0'
    implementation 'org.apache.commons:commons-text:+'
    testImplementation 'junit:junit:4.12'
}

generateGrammarSource {
    outputDirectory = file("${project.rootDir}/src/main/java/org/sampl/antlr")
    arguments += ['-package', 'org.sampl.antlr', '-no-listener', '-visitor']
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}

compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.sampl.Main'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
