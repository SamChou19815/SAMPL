buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'org.sampl'
version '0.0.1'

apply plugin: 'antlr'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    antlr "org.antlr:antlr4:4.5" // use ANTLR version 4
    compile 'com.github.SamChou19815:okaml-lib:0.3.2'
    compile 'org.apache.commons:commons-text:+'
    testCompile 'junit:junit:4.12'
}

generateGrammarSource {
    outputDirectory = file("${project.rootDir}/src/main/java/org/sampl/antlr")
    arguments += ['-package', 'org.sampl.antlr', '-no-listener', '-visitor']
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=enable"]
    }
}

compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.sampl.Main'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
